#!/usr/bin/env nu

const dconf_path = "/org/gnome/desktop/interface/color-scheme"
const prefer_light = "'prefer-light'"
const prefer_dark = "'prefer-dark'"

def set-scheme [scheme] {
  if $env.XDG_CURRENT_DESKTOP == "niri" {
    niri msg action do-screen-transition --delay-ms 250
  }

  dconf write $dconf_path $scheme
}

def get-current-scheme [] {
  dconf read $dconf_path
}

# Toggle color scheme.
def "main toggle" [] {
  let old_scheme = get-current-scheme
  let new_scheme = if ($old_scheme == $prefer_light) {
    $prefer_dark
  } else {
    $prefer_light
  }

  set-scheme $new_scheme
}

# Set color scheme to dark.
def "main dark" [] {
  set-scheme $prefer_dark
}

# Set color scheme to light.
def "main light" [] {
  set-scheme $prefer_light
}

# Return a Waybar module.
def "main waybar" [] {
  # work around Wayland bug https://github.com/Alexays/Waybar/pull/1784
  # I'm not sending signals, what fucking year is this?
  sleep 0.5sec

  let current_scheme = get-current-scheme

  if ($current_scheme == $prefer_light) {
    { text: "󰖨", class: "colorscheme-light" } | to json -r
  } else {
    { text: "󰽧", class: "colorscheme-dark" } | to json -r
  }
}

def main [] {
  help main
}
