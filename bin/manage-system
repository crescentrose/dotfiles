#!/usr/bin/env nu

use vlog.nu *

let FLAKE_DIR = $"($env.HOME)/Code/dotfiles/nixos"
let SYSTEM_NAME = "streaming-heart"
let FLAKE_NAME = $"($FLAKE_DIR)#($SYSTEM_NAME)"

# Update the Nix flake version.
def "main update" [] {
  vlog info $"üöö Updating flake ($FLAKE_NAME)..."
  vlog timed "Update" {
    nix flake update --flake $FLAKE_DIR
  }
}

# Build a new derivation and switch to it.
def "main rebuild" [] {
  vlog timed "Rebuild" {
    vlog info $"üè† Building flake ($FLAKE_NAME)..."
    sudo nixos-rebuild --flake $FLAKE_NAME --impure switch
  }
}

# Clean up garbage and optimise Nix store. (Most of the time this should happen automatically,
# though.)
def "main clean-up" [] {
  vlog timed "Clean-up" {
    vlog info "‚ôª Running garbage collection..."
    sudo nix-collect-garbage --delete-old
    vlog info "ü´ô Running store optimisation..."
    sudo nix-store --optimise
  }
}

# Show the latest logs for a (root) systemd unit in reverse chronological order.
def "main inspect" [service: string, --root (-r)] {
  vlog info $"ü§® Inspecting ($service)..."
  vlog timed "Service inspection" {
    if $root {
      sudo journalctl -u $service --reverse --since="15m ago"
    } else {
      journalctl --user -u $service --reverse --since="15m ago"
    }
  }
}

def main [] {
  help main
}

# Logger
