# @crescentrose's zsh file
# not nearly as buff as `vimrc` ðŸ˜°

export PATH="$HOME/bin:$PATH"

# Custom functions {{{
# Ensure that the files we want to source actually exist.
function safe_source {
  test -e "$1" && source "$1"
}

function zsh_minisnip {
  local snippet sub subs replacement key
  snippet=$(minisnip)
  LBUFFER=$snippet
  zle reset-prompt

  # Get any substitutions from the snippet
  subs=($(egrep -o "{:[a-zA-Z0-9]+:}" <<< "$snippet"))

  if [[ -n "$subs" ]]; then
    # Run through all placeholders
    # ${(u)subs[@]} means only unique members of the array
    for sub in ${(u)subs[@]}; do
      local subname=$(sed -E "s/[{:}]//g" <<< "$sub")
      zle -R -c "$subname: _" # underscore is a hacky way of displaying a cursor
      read -k key
      # lifted almost verbatim from the zsh manual
      while [[ '#key' -ne '##\n' && '#key' -ne '##\r' ]]; do
        if [[ '#key' -eq '##^?' || '#key' -eq '##^h' ]]; then
          [[ -n $replacement ]] && replacement=${replacement[1,-2]}
        elif [[ '#key' -eq '##\C-g' ]]; then
          return 1
        else
          replacement="${replacement}${key}"
        fi
        zle -R -c "$subname: ${replacement}_"
        read -k key || return 1
      done

      # perform the replacement, clean up and display it in the buffer
      snippet=$(sed -E "s/$sub/$replacement/g" <<< "$snippet")
      replacement=
      key=
      LBUFFER=$snippet
      zle reset-prompt
    done
  fi
}

# }}}

# Plugins, using zplug: https://github.com/zplug/zplug {{{
# Enable zplug
export ZPLUG_HOME=/usr/local/opt/zplug
source $ZPLUG_HOME/init.zsh

# zsh-async: dependecy for many other plugins
zplug mafredri/zsh-async, from:github
# pure: nice terminal prompt
zplug sindresorhus/pure, use:pure.zsh, from:github, as:theme
# zsh-syntax-highlighting: self-descriptive
zplug 'zsh-users/zsh-syntax-highlighting', defer:2
# zsh-completions: approximately one billion completion scripts
zplug 'zsh-users/zsh-completions', defer:2
zplug 'zsh-users/zsh-autosuggestions', defer:2

zplug load 

# Plugins end }}}

# Shell integrations and includes {{{
# Include custom aliases from a separate file so that we can change them easily
safe_source ~/.aliasrc

# Include Google Cloud SDK stuff
safe_source '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc'
safe_source '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/completion.zsh.inc'

# engage rbenv if it is installed
if command -v 'rbenv' >/dev/null; then
  RBENV="$(which rbenv)"
  eval "$($RBENV init -)"
fi

# Engage FZF
safe_source "$HOME/.fzf.zsh"

# Load NVM
eval "$(fnm env)"

# use Linuxbrew if on Linux
if [[ -f "/home/linuxbrew/.linuxbrew/bin/brew" ]] then
  eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
fi

# automatically change kitty colors based on time of day
if command -v "sunshine" >/dev/null; then
  if [[ "$(sunshine -s "#Rijeka, Croatia")" = "day" ]]; then
    chtheme light
  else
    chtheme dark
  fi
fi
# }}}

# Environment variables {{{
# Enable history
export HISTSIZE=10000
export SAVEHIST=10000
export HISTFILE=~/.zsh_history

# Default configuration for `fzf`
export FZF_DEFAULT_OPTS="\
  --ansi \
"
export FZF_DEFAULT_COMMAND="ag -l --nocolor -g \"\""

# Default configuration for `bat`
export BAT_THEME="OneHalfDark"

# make sure GPG can do its thing on macOS
export GPG_TTY=$(tty)

export CDPATH=".:$HOME/Code"

export LANG=en_US.UTF-8

# Google :(
export CLOUDSDK_PYTHON=python2
# }}}

# Autocompletion {{{
autoload -Uz compinit
compinit

# Completion for kitty
if [[ "$TERM" = "xterm-kitty" ]]; then
  kitty + complete setup zsh | source /dev/stdin
fi
# }}}

# Custom key bindings {{{
autoload -U edit-command-line
zle -N edit-command-line
bindkey '^xe' edit-command-line
bindkey '^x^e' edit-command-line

zle -N zsh_minisnip
bindkey "^T" zsh_minisnip
# }}}

# vim: fdm=marker fdl=0
